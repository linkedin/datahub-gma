// TODO actually hook up to a CI. Until we hook up a CI follow these steps:
// 1. Check out the repo and sync to the commit you want to release.
// 2. Build with `./gradlew build`.
// 3. Publish to your local maven with `./gradlew publishToMavenLocal`.
// 4. Verify the version is correct (inspect generated poms or the contents of your ~/.m2 directory).
// 5. Publish to bintray with `./gradlew bintrayUploadAll`.
// 6. Tag the commit by running `git tag "v$version"`, replacing $version with the semver.
// 7. Push the tag by running `git push --tags`
// 8. Go to GitHub and make a release from the tag.
// 9. Go to bintray and click "publish" on the newly uploaded version.

// Task to fail fast and clearly if required properties are not set.
task verifyBintrayProperties {
  doFirst {
    if (!project.hasProperty('bintray.dryRun')) {
      if (System.getenv('BINTRAY_USER') == null) {
        throw new Exception("Environment variable BINTRAY_USER not set.");
      }
      if (System.getenv('BINTRAY_KEY') == null) {
        throw new Exception("Environment variable BINTRAY_USER not set.");
      }
    }
  }
}

task bintrayUploadAll {
  description = "Runs 'bintrayUpload' tasks from all projects"
}

allprojects {
  tasks.matching { it.name == "bintrayUpload" }.all {
    it.dependsOn verifyBintrayProperties
    bintrayUploadAll.dependsOn it
  }
}