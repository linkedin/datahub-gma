apply plugin: 'java'
apply plugin: 'pegasus'

// Don't publish the "normal" jar since it'd be empty anyway
project.ext.publications = ['dataTemplate']

apply from: "$rootDir/gradle/java-publishing.gradle"

pegasus.main.generationModes = [PegasusGenerationMode.PEGASUS, PegasusGenerationMode.AVRO]

dependencies {
  dataModel project(':core-models')
  dataModel project(':dao-api')

  testCompile externalDependency.assertJ

  testCompile externalDependency.junitJupiterApi
  testCompile externalDependency.junitJupiterEngine
  testCompile externalDependency.junitJupiterParams
}

test {
  useJUnitPlatform()
}

// Seems like the pegasus plugin prevents us from just adding the pegasus dir directly as a resource...
task copyPdl(type: Copy) {
  from 'src/main/pegasus'
  into "$buildDir/resources"
}

sourceSets {
  main {
    resources {
      srcDir files("$buildDir/resources").builtBy(copyPdl)
    }
  }
}

sourceSets.mainGeneratedDataTemplate.java.srcDirs('src/main/java/com/linkedin/testing/urn')

clean {
  project.delete("mainGeneratedAvroSchema")
  project.delete("mainGeneratedDataTemplate")
}